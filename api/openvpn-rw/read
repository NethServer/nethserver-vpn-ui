#!/usr/bin/perl

#
# Copyright (C) 2019 Nethesis S.r.l.
# http://www.nethesis.it - nethserver@nethesis.it
#
# This script is part of NethServer.
#
# NethServer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License,
# or any later version.
#
# NethServer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with NethServer.  If not, see COPYING.
#

use strict;
use warnings;
use esmith::ConfigDB;
use esmith::NetworksDB;
use NethServer::Service;
use NetAddr::IP;
use JSON;
use File::Temp qw/tempfile/;
use MIME::Base64;
use MIME::Lite;
use Net::Domain qw(domainname hostfqdn);

require '/usr/libexec/nethserver/api/nethserver-vpn-ui/lib/vpn_functions.pl';

sub find_host {
    my $db = shift;
    my $ip = shift;

    foreach ($db->get_all_by_prop('type' => 'host')) {
        return $_->key if ($_->prop('IpAddress') eq $ip);
    }

    return '';
}

my $input = readInput();
my $cmd = $input->{'action'};

my $ret = {};

if ($cmd eq 'users') {

    my @tmp;
    my $users = safe_decode_json(`/usr/libexec/nethserver/list-users`);
    foreach (keys %$users) {
        my $short = $_;
        $short =~ s/(@.*)$//;
        push(@tmp, {'name' =>  $_, 'shortname' => $short, 'gecos' => $users->{$_}{'gecos'}});
    }
    @tmp = sort { $a->{'shortname'} cmp $b->{'shortname'} } @tmp;
    $ret->{'users'} = \@tmp;;

} elsif ($cmd eq 'interfaces') {

    my @interfaces;
    my $ndb = esmith::NetworksDB->open_ro();
    foreach ($ndb->interfaces()) {
        next if (!$_->prop('role'));
        next if ($_->prop('type') ne 'bridge'); 
        my $bootproto = $_->prop('bootproto') || 'none';
        if ($bootproto eq 'none') {
            push(@interfaces, {"name" => $_->key, "address" => $_->prop('ipaddr')});
        } else {
            push(@interfaces, {"name" => $_->key, "address" => 'dhcp'});
        }
    }

    $ret->{'interfaces'} = \@interfaces;

} elsif ($cmd eq 'configuration') {

    my $db = esmith::ConfigDB->open_ro();
    my $ndb = esmith::ConfigDB->open_ro('networks');
    my $record = $db->get('openvpn@host-to-net');
    my $accountProvider = ($db->get_prop('sssd', 'Provider') ne '') ? JSON::true : JSON::false;
    my $mode = $record->prop('Mode') || '';
    my $dnsServers = $db->get_prop('dns','NameServers')|| '';
    my $domainName = $db->get_value('DomainName') || '';
    my @dns;
    my $s = new NethServer::Service('dnsmasq');
    if( $s->is_owned() && $s->is_enabled() ) {
        if ($mode eq 'bridged') { # use green address
            foreach ($ndb->get_all_by_prop('role' => 'green')) {
                push(@dns, $_->prop('ipaddr'));
            }
        } else { # use tun address
            my ($tot, $first, $last) = esmith::util::computeHostRange($record->prop('Network'),  $record->prop('Netmask'));
            $first = esmith::util::IPquadToAddr($first); # convert to integer
            $first = esmith::util::IPaddrToQuad($first+1);
            push(@dns, $first);
        }
    } else {
        @dns = split(',', $dnsServers, 2);
    }

    if (@dns) {
        $ret->{'Placeholders'}{Dns} = $dns[0];
        $ret->{'Placeholders'}{DomainName} = $domainName;
    } else {
        $ret->{'Placeholders'} = {Dns => "", DomainName => ""};
    }

    if ($record) {
        my %props = $record->props;
        foreach (qw(type access TapInterface)) {
            delete($props{$_});
        }
        if (defined($props{'UDPPort'}) && $props{'UDPPort'} ne '') {
            $props{'Protocol'} = 'udp';
            $props{'Port'} = $props{'UDPPort'};
            delete($props{'UDPPort'});
        }
        if (defined($props{'TCPPort'}) && $props{'TCPPort'} ne '') {
            $props{'Protocol'} = 'tcp';
            $props{'Port'} = $props{'TCPPort'};
            delete($props{'TCPPort'});
        }
        foreach (qw(Remote CustomRoutes)) {
            $props{$_} = [split(",",$props{$_})];
        }

        # generate default values for Remote
        if(scalar @{$props{'Remote'}} == 0) {
            $props{'Remote'} = get_public_addresses();
        }
        $props{'AccountProvider'} = $accountProvider;
        $ret->{'configuration'} = \%props;
    } else {
        $ret->{'configuration'} = {Port => "-", Remote => [], CustomRoutes => [], AccountProvider => $accountProvider, Protocol => 'udp', 'status' => 'disabled'};
    }

} elsif ($cmd eq 'algorithms') {

    $ret->{'algorithms'} = openvpn_algorithms();

} elsif ($cmd eq 'accounts') {

    my $db = esmith::HostsDB->open_ro();
    my $accounts_status = safe_decode_json(`/usr/libexec/nethserver/openvpn-status3 /var/spool/openvpn/host-to-net 2>/dev/null`);
    my $accounts = safe_decode_json(`/usr/libexec/nethserver/api/nethserver-vpn-ui/openvpn-rw/list-accounts 2>/dev/null`);
    my $last_connected = safe_decode_json(`/usr/libexec/nethserver/api/nethserver-vpn-ui/openvpn-rw/accounts-last-connected 2>/dev/null`);

    foreach (@{$accounts}) {
        my $host = find_host($db, $_->{'OpenVpnIp'}) || '';
        my $account_name = $_->{'name'};
        $_->{'Host'} = $host;
        if ($accounts_status->{$account_name}) {
            $_->{'statistics'} = $accounts_status->{$account_name};
        } else {
            $_->{'statistics'} = undef;
        }
        my ($item) = grep {
            $_->{'account'} eq $account_name
        } @{$last_connected};
        $_->{'lastConnected'} = $item->{'lastConnected'} || '';
    }
    $ret->{'accounts'} = $accounts;

} elsif ($cmd eq 'connectionHistory') {

    my $account = $input->{'account'};
    my $time_interval = $input->{'timeInterval'};
    my $connection_history = safe_decode_json(`/usr/libexec/nethserver/api/nethserver-vpn-ui/openvpn-rw/connection-history '$account' '$time_interval' 2>/dev/null`);
    $ret->{'connectionHistory'} = $connection_history;

} elsif ($cmd eq 'download') {

    if ($input->{'type'} eq 'ovpn') {
        my $name = $input->{'name'};
        $ret->{'data'} = encode_base64(`/usr/libexec/nethserver/openvpn-local-client '$name'`);
        $ret->{'filename'} = $input->{'name'}.".ovpn";
    } elsif ($input->{'type'} eq 'pem') {
        my $name = $input->{'name'};
        $ret->{'data'} = encode_base64(`cat /var/lib/nethserver/certs/$name.key /var/lib/nethserver/certs/$name.crt /etc/pki/tls/certs/NSRV.crt`);
        $ret->{'filename'} = $input->{'name'}.".pem";
    } elsif ($input->{'type'} eq 'pkcs12') {
        my $name = $input->{'name'};
        $ret->{'data'} = encode_base64(`cat /var/lib/nethserver/certs/$name.p12`);
        $ret->{'filename'} = $input->{'name'}.".p12";
    } elsif ($input->{'type'} eq 'connectionHistoryCsv') {
        $ret->{'data'} = encode_base64(`/usr/libexec/nethserver/api/nethserver-vpn-ui/openvpn-rw/connection-history-csv`);
        $ret->{'filename'} = $input->{'name'}.".csv";
    } else {
        error();
    }

} elsif ($cmd eq 'mail') {

    my $db = esmith::ConfigDB->open_ro();
    my $sender = $db->get_prop('root','SenderAddress') || 'no-reply@'.domainname();
    my $remote = $db->get_prop('openvpn@host-to-net', 'Remote') || '';
    my $name = $input->{'name'};
    # use first remote entry or the fqdn
    if ($remote) {
        $remote =~ s/,(.*)$//;
    } else {
        $remote = hostfqdn();
    }


    my $text = "Hello $name,\nthis email contains the configuration file for your OpenVPN connection.
First, you have to install the OpenVPN desktop client or mobile app. You can find the download links at the end this message.
Please read the instructions for your specific device.

**Microsoft systems (OpenVPN client)**
Save the attached file and copy it to the configuration folder.
You can fin the configuration folder from the menu 'Start' -> 'OpenVPN' -> 'OpenVPN Configuration file directory'.
Click on the icon with the 2 screens at the bottom right to start the connection.

**macOS systems (Tunnelblick client)**
Save the attached file, double-click and add it to your Tunnelblick profiles.

**Android and iOS (OpenVPN Connect)**
Click on the attachment, the system will activate the app automatically and will ask you to import the file.

Downloads
Windows: https://openvpn.net/community-downloads
macOS: https://tunnelblick.net/
Android: https://play.google.com/store/apps/details?id=net.openvpn.openvpn
iOS: https://itunes.apple.com/it/app/openvpn-connect/id590379981
";

    # Allow mail override
    # The configuration file must contain the mail text.
    # A $name variable will be replaced with the actual account name
    my $config_file = "/etc/nethserver/openvpn-mail.cfg";
    if (-f $config_file) {
        # slurp file
        my $file_content = do{ local(@ARGV,$/) = $config_file; <> };
        # replace $name variable
        $file_content =~ s/\$name/$name/g;
        if ($file_content) {
            $text = $file_content;
        }
    }

    my $msg = MIME::Lite->new(
        From    => $sender,
        To      => $input->{'address'},
        Subject => "Your VPN access file for $remote",
        Type    => 'multipart/mixed'
    );
    $msg->attr('content-type.charset' => 'UTF8');

    $msg->attach(
        Type     => 'TEXT',
        Data     => $text
    );

    # configuration file
    $msg->attach(
        Type     => 'application/x-openvpn-profile',
        Data     => [`/usr/libexec/nethserver/openvpn-local-client '$name'`],
        Filename => "$name.ovpn",
        Disposition => 'attachment'
    );

    if ($msg->send()) {
        success();
    } else {
        error();
    }
} elsif ($cmd eq 'pending-changes') {
    # check for pending changes that need a service restart
    $ret->{'pendingChanges'} = (-e '/var/spool/openvpn/road-warrior-lock') ? 1 : 0;
} else {
    error();
}

print encode_json($ret);
